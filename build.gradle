buildscript {
    repositories {
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.9.0'
    }
}

plugins {
    id 'maven-publish'
    id 'signing'
}

ext {
    groupId = 'io.github.sandeep5193'
    webrtcVersion = '134'
    libraryVersion = '134.0.0'
}

apply plugin: 'com.android.library'

android {
    namespace = 'dev.bargen.webrtc'

    compileSdkVersion 35
    buildToolsVersion '35.0.0'

    defaultConfig {
        minSdkVersion 21
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    publishing {
        singleVariant('release')
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation files('libs/libwebrtc.jar')
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'io.github.sandeep5193'
                artifactId = 'webrtc-android'
                version = "134.0.0"

                from components.findByName('release')

                pom {
                    name = 'WebRTC for Android'
                    description = "WebRTC M${project.ext.webrtcVersion} builds for Android, without any patches applied."
                    url = 'https://github.com/sandeep5193/webrtc-android'

                    scm {
                        url = 'https://github.com/sandeep5193/webrtc-android'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'LICENSE-APACHE'
                        }
                        license {
                            name = 'The MIT License'
                            url = 'LICENSE-MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'sd'
                            name = 'Sandeep Dhameshia'
                            email = 'sandeep.dhameshia@gmail.com'
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }
}
